Процедура записатьВИсториюЗначениеРеквизитаОбъекта(Объект, реквизит, значениеРеквизита, Знач номерВерсии = 0)
	менеджерЗаписи = РегистрыСведений.улкИсторияРеквизитовОбъектов.СоздатьМенеджерЗаписи();	
	значениеДляЗаписи = новый ХранилищеЗначения(значениеРеквизита, новый СжатиеДанных(-1));
	менеджерЗаписи.ЗначениеРеквизита = значениеДляЗаписи;
	менеджерЗаписи.Реквизит = реквизит;
	менеджерЗаписи.Объект = Объект;
	менеджерЗаписи.Период = ТекущаяДата();
	менеджерЗаписи.Автор = ПараметрыСеанса.ТекущийПользователь;
	менеджерЗаписи.НомерВерсии = номерВерсии + 1;
	менеджерЗаписи.Записать(Истина);
КонецПроцедуры

//ivan начало 05.12.2017 11:49:06
функция нужноЗаписыватьИсторию(текущееЗначение, прошлоеЗначение)
	Если ТипЗнч(текущееЗначение) = Тип("ТаблицаЗначений") И ТипЗнч(прошлоеЗначение) = Тип("ТаблицаЗначений") Тогда
		хэшТекущегоЗначения = улкОбщийМодуль.получитьХэшMD5(ЗначениеВСтрокуВнутр(текущееЗначение));
		хэшПрошлогоЗначения = улкОбщийМодуль.получитьХэшMD5(ЗначениеВСтрокуВнутр(прошлоеЗначение));
		Если ПустаяСтрока(хэшТекущегоЗначения) ИЛИ ПустаяСтрока(хэшПрошлогоЗначения) Тогда
			Возврат Ложь;
		КонецЕсли;
		Возврат хэшТекущегоЗначения <> хэшПрошлогоЗначения;
	КонецЕсли;
	Возврат текущееЗначение <> прошлоеЗначение;
КонецФункции
//ivan конец 05.12.2017 11:49:06

//ivan начало 04.12.2017 12:54:31
функция получитьСтруктуруЗначенийПолейИТабличныхЧастейОбъекта(объектКонфигурации) Экспорт
	структураРезультат = новый Структура;
	Для каждого полеРеквизита из объектКонфигурации.Метаданные().Реквизиты Цикл
		значениеПоля = объектКонфигурации[полеРеквизита.Имя];
		Если НЕ ЗначениеЗаполнено(значениеПоля) Тогда
			Продолжить;
		КонецЕсли;
		структураРезультат.Вставить(полеРеквизита.Имя, значениеПоля);	
	КонецЦикла;
	Для каждого полеРеквизита из объектКонфигурации.Метаданные().ТабличныеЧасти Цикл
		табличнаяЧасть = объектКонфигурации[полеРеквизита.Имя];
		Если табличнаяЧасть.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		структураРезультат.Вставить(полеРеквизита.Имя, объектКонфигурации[полеРеквизита.Имя].Выгрузить());	
	КонецЦикла;
	Для каждого полеРеквизита из объектКонфигурации.Метаданные().СтандартныеРеквизиты Цикл
		значениеПоля = объектКонфигурации[полеРеквизита.Имя];
		Если НЕ ЗначениеЗаполнено(значениеПоля) Тогда
			Продолжить;
		КонецЕсли;
		структураРезультат.Вставить(полеРеквизита.Имя, значениеПоля);	
	КонецЦикла;
	Возврат структураРезультат;
КонецФункции
//ivan конец 04.12.2017 12:54:31

//ivan начало 05.12.2017 11:10:58
функция получитьДанныеПоследнейВерсииОбъекта(ссылкаНаОбъект)Запрос = Новый Запрос;
	тзРезультат = новый ТаблицаЗначений;
	тзРезультат.Колонки.Добавить("Реквизит", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(255));
	тзРезультат.Колонки.Добавить("ЗначениеРеквизита");
	тзРезультат.Колонки.Добавить("НомерВерсии", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10, 0));
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсторияРеквизитов.Реквизит,
	               |	ИсторияРеквизитов.ЗначениеРеквизита,
	               |	ИсторияРеквизитов.НомерВерсии
	               |ИЗ
	               |	РегистрСведений.улкИсторияРеквизитовОбъектов.СрезПоследних(&ДатаПолученияЗначения, Объект = &ОбъектСсылка) КАК ИсторияРеквизитов";
	
	Запрос.УстановитьПараметр("ДатаПолученияЗначения", ТекущаяДата());
	Запрос.УстановитьПараметр("ОбъектСсылка", ссылкаНаОбъект);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		новаяСтрока = тзРезультат.Добавить();
		новаяСтрока.Реквизит = Выборка.Реквизит;
		новаяСтрока.ЗначениеРеквизита = Выборка.ЗначениеРеквизита.Получить();
		новаяСтрока.НомерВерсии = Выборка.НомерВерсии;
	КонецЦикла;
	Возврат тзРезультат;	
КонецФункции

Процедура записатьДанныеОбъектаВИсторию(Объект, структураПолейОбъекта) Экспорт
	Если НЕ Константы.улкХранитьИсториюРеквизитовОбъектов.Получить() Тогда
		Возврат;
	КонецЕсли;
	данныеПоследнейВерсии = получитьДанныеПоследнейВерсииОбъекта(Объект.ссылка);
	Для каждого элементСтруктуры Из структураПолейОбъекта Цикл
		значениеИзИстории = Неопределено;
		текущаяВерсияЗначенияРеквизита = данныеПоследнейВерсии.Найти(элементСтруктуры.Ключ, "Реквизит");
		номерВерсии = 0;
		Если текущаяВерсияЗначенияРеквизита <> Неопределено Тогда
			Если НЕ нужноЗаписыватьИсторию(элементСтруктуры.Значение, текущаяВерсияЗначенияРеквизита.ЗначениеРеквизита) Тогда
				Продолжить;
			КонецЕсли;
			номерВерсии = текущаяВерсияЗначенияРеквизита.НомерВерсии;
		КонецЕсли;
		записатьВИсториюЗначениеРеквизитаОбъекта(Объект.ссылка, элементСтруктуры.Ключ, элементСтруктуры.Значение, номерВерсии); 
	КонецЦикла; 	
КонецПроцедуры
//ivan конец 05.12.2017 11:10:58

//ivan начало 06.12.2017 11:00:32
функция получитьЗначениеРеквизитаИзИстории(ссылкаНаОбъект, Реквизит, датаПолученияЗначения) Экспорт
	Если НЕ Константы.улкХранитьИсториюРеквизитовОбъектов.Получить() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	структураОтбора = новый Структура("Объект,Реквизит", ссылкаНаОбъект, Реквизит);
	наборЗаписей = РегистрыСведений.улкИсторияРеквизитовОбъектов.ПолучитьПоследнее(датаПолученияЗначения, структураОтбора);
	результат = наборЗаписей.ЗначениеРеквизита.Получить();
	Если наборЗаписей.Номерверсии = 0 Тогда
		Если полеЕстьВРеквизитахУОбъекта(ссылкаНаОбъект, Реквизит) Тогда
			результат = ссылкаНаОбъект[Реквизит];
		КонецЕсли;
	КонецЕсли;
	Возврат результат;		
КонецФункции

функция полеЕстьВРеквизитахУОбъекта(объектКонфигурации, имяПоля)
	результат = Ложь;
	Для каждого полеРеквизита из объектКонфигурации.Метаданные().Реквизиты Цикл
		Если имяПоля = полеРеквизита.Имя Тогда
			результат = Истина;
		КонецЕсли;
	КонецЦикла;
	Для каждого полеРеквизита из объектКонфигурации.Метаданные().ТабличныеЧасти Цикл
		Если имяПоля = полеРеквизита.Имя Тогда
			результат = Истина;
		КонецЕсли;
	КонецЦикла;
	Для каждого полеРеквизита из объектКонфигурации.Метаданные().СтандартныеРеквизиты Цикл
		Если имяПоля = полеРеквизита.Имя Тогда
			результат = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат результат;
КонецФункции

процедура дополнитьСтруктуруЗначенийПолейКонтактнойИнформацией(структураЗначенийПолей, объект) Экспорт
	наборЗаписиРегистраКонтактнаяИнформация = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	наборЗаписиРегистраКонтактнаяИнформация.Отбор.Объект.Значение = объект;
	наборЗаписиРегистраКонтактнаяИнформация.Отбор.Объект.Использование = Истина;
	наборЗаписиРегистраКонтактнаяИнформация.Прочитать();
	Для каждого записьРегистра Из наборЗаписиРегистраКонтактнаяИнформация Цикл
		Если ПустаяСтрока(записьРегистра.Представление) Тогда
			Продолжить;
		КонецЕсли;
		//ivan начало 08.12.2017 15:32:00
		Если ТипЗнч(записьРегистра.Вид) = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
			наименованиеКлюча = СтрЗаменить(СокрЛП(записьРегистра.Вид.Наименование), " ", "");
		Иначе
			наименованиеКлюча = СтрЗаменить(СокрЛП(записьРегистра.Вид), " ", "");
		КонецЕсли;
		Попытка
			структураЗначенийПолей.Вставить(наименованиеКлюча, СокрЛП(записьРегистра.Представление));	
		Исключение
			Продолжить;
		КонецПопытки;
		//ivan конец 08.12.2017 15:32:00
	КонецЦикла;
КонецПроцедуры
//ivan конец 06.12.2017 11:00:32
